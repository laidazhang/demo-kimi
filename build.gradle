apply from: file("${rootDir}/gradle/project.gradle")

subprojects {
    group = 'core.demo'
    version = '1.0.0'

    repositories {
        maven {
            url 'https://neowu.github.io/maven-repo/'
            content {
                includeGroupByRegex 'core\\.framework.*'
            }
        }
    }
}

def coreNGVersion = '7.3.0'
def hsqlVersion = '2.5.0'

configure(subprojects.findAll { it.name.endsWith('-db-migration') }) {
    apply from: file("${rootDir}/gradle/db-migration.gradle")

    dependencies {
        runtimeOnly 'mysql:mysql-connector-java:8.0.17'
    }
}

configure(subprojects.findAll { it.name.endsWith('-mongo-migration') }) {
    apply from: file("${rootDir}/gradle/app.gradle")
    dependencies {
        implementation "core.framework:core-ng:${coreNGVersion}"
        implementation "core.framework:core-ng-mongo:${coreNGVersion}"
    }
    tasks.register('mongoMigrate') {
        dependsOn run
    }
}

configure(subprojects.findAll { it.name.matches('.*-interface') }) {
    dependencies {
        implementation "core.framework:core-ng-api:${coreNGVersion}"
    }
}

configure([project(':demo-service'),
           project(':demo-site'),
           project(':website'),
           project(':back-office'),
           project(':customer-service'),
           project(':order-service')]) {
    apply from: file("${rootDir}/gradle/app.gradle")
    dependencies {
        implementation "core.framework:core-ng:${coreNGVersion}"
        testImplementation "core.framework:core-ng-test:${coreNGVersion}"
    }
}

project(':demo-service') {
    dependencies {
        runtimeOnly 'mysql:mysql-connector-java:8.0.17'
        testRuntimeOnly "org.hsqldb:hsqldb:${hsqlVersion}"
        implementation "core.framework:core-ng-mongo:${coreNGVersion}"
        implementation project(':demo-service-interface')
    }
}

project(':demo-site') {
    dependencies {
        implementation project(':demo-service-interface')
    }
}

project(':customer-service') {
    dependencies {
        runtimeOnly 'mysql:mysql-connector-java:8.0.17'
        testRuntimeOnly "org.hsqldb:hsqldb:${hsqlVersion}"
        implementation project(':customer-service-interface')
    }
}

project(':order-service') {
    dependencies {
        runtimeOnly 'mysql:mysql-connector-java:8.0.17'
        testRuntimeOnly "org.hsqldb:hsqldb:${hsqlVersion}"
        implementation project(':order-service-interface')
    }
}

project(':website') {
    dependencies {
        implementation project(':customer-service-interface')
        implementation project(':order-service-interface')
    }
}

project(':back-office') {
    dependencies {
        implementation project(':customer-service-interface')
        implementation project(':order-service-interface')
        implementation project(':back-office-interface')
    }
}